apply plugin: "groovy"
apply plugin: "java"
apply plugin: "application"
apply plugin: "idea"

/**************************************************
 *
 * SETUP
 *
 **************************************************/
buildscript {
    ext {
        groovyVersion = '2.1.3'
    }
    repositories {
        mavenCentral()
        maven {
            url 'http://repo.spring.io/milestone'
        }
    }
    dependencies {
//        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.12'
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

/*************************
 * SOURCE SET
 *************************/
sourceSets {
    main {
        groovy {
            srcDirs = ['src/main/java']
        }
    }

    test {
        groovy {
            srcDirs = ['src/test/java']
        }
    }
}

/**************************************************
 *
 * Define Variable&Function
 *
 **************************************************/
sourceCompatibility = "1.6"
targetCompatibility = "1.6"

String projectName = "installer-maker"
version = "git describe --tags".execute([], project.projectDir).text.trim()
applicationName = projectName
group = "jaemmisseo"
mainClassName = "install.Starter"

String distributionPath = "${buildDir}/distributions"
String libFileName = "${projectName}-${version}"
String SystemEnv_INSTALLER_MAKER_HOME = "$System.env.INSTALLER_MAKER_HOME"
boolean modeRemoteDependencies = project.hasProperty('remote')
boolean hadDeployment = false
List<Dependency> moduleList = []

ext.getString = { String propertyName -> return project.hasProperty(propertyName) ? project.property(propertyName) : null }
ext.getBoolean = { String propertyName -> return project.hasProperty(propertyName) && (!project.property(propertyName) || project.property(propertyName) == 'true') }
ext.getDependency = { String dependencyString ->
    String name = dependencyString.split(':').toList()[1]
    return (modeRemoteDependencies) ? dependencyString : project(":${name}")
}

static void printSubject(String subjectName){
    println "\n========================="
    println "= ${subjectName}"
    println "========================="
}

static int getLongestLength(List list, String fieldName){
    return getLongestLength( list.collect{it[fieldName]} )
}

static int getLongestLength(List<String> list){
    int longestLength = -1
    list.each{ def text ->
        int length = text?.length()
        if (longestLength < length)
            longestLength = length
    }
    return longestLength
}

static String getSpacesToLineUp(String stringItem, int bestLongerLength){
    return (stringItem.length() < bestLongerLength) ? (1..(bestLongerLength - stringItem.length())).collect{' '}.join('') : ''
}

static Dependency getDependencyFromDependencyString(String dependencyString){
    List projectItems = dependencyString.split(':').toList()
    return new Dependency(dependencyString: dependencyString, path:projectItems[0], name:projectItems[1], branchName:'', version:projectItems[2] ?: '')
}

static String getUserSetedVersionFromModuleList(List dependencyStringList, String moduleName){
    String dependencyString = dependencyStringList.find{ String dependencyString ->
        Dependency dependency = getDependencyFromDependencyString(dependencyString)
        return dependency.name == moduleName
    }
    return (dependencyString) ?: ''
}

class Dependency {
    String dependencyString
    String path
    String name
    String branchName
    String version
}

/**************************************************
 *
 * Dependencies
 *
 **************************************************/
List<String> subprojectList = [
        'com.github.avaj-java:common-util:0.2.0',
        'com.github.avaj-java:common-man:0.2.0',
        'com.github.avaj-java:fileman:0.2.0',
        'com.github.avaj-java:reportman:0.0.3',
        'com.github.avaj-java:restman:0.0.3'
]
List<String> dependencyList = [
        'org.codehaus.groovy:groovy-all:2.1.3',
        'com.sun.mail:javax.mail:1.5.5',
        "com.oracle:ojdbc6:11.2.0.3",
        'org.yaml:snakeyaml:1.18',
]
repositories {
    jcenter()
    maven { url "https://jitpack.io" }
    maven { url 'http://repo.spring.io/milestone' }
    maven { url "http://maven.springframework.org/release" }
}
dependencies {
    //JAEMISSEO
    subprojectList.each{ compile(getDependency(it)) }
    //Others
    dependencyList.each{ compile(it) }
    //Test
    testCompile "junit:junit:4.11"
}

/**************************************************
 *
 * Setup Each Sub Projects
 *
 **************************************************/
subprojects {
    /*************************
     * Gethering Module's Git Info
     *************************/
    moduleList << new Dependency(
            name        : project.name,
            branchName  : 'git rev-parse --abbrev-ref HEAD'.execute([], project.projectDir).text.trim(),
            version     : 'git describe --tags'.execute([], project.projectDir).text.trim()
    )
    group = 'jaemmisseo'
    version = moduleList[(moduleList.size()-1)].version

    //Message to Sub Gradle
    ext.setProperty('modeParentRemoteDependencies', modeRemoteDependencies)
}

if (modeRemoteDependencies){
    moduleList = subprojectList.collect{ String dependencyString -> getDependencyFromDependencyString(dependencyString) }
}


/**************************************************
 *
 * Print Information
 *
 **************************************************/
/*************************
 * Print Java Version
 *************************/
printSubject "Java"
println ' - Compile Environment '
exec{ executable 'java';  args "-version"; }

/*************************
 * Print Module's Git Info
 *************************/
printSubject "Git"
// Get best long length 'moduleName' and 'branchName' to adjust spacing
int bestLongModuleNameLength = getLongestLength(moduleList, 'name')
int bestLongBranchNameLength = getLongestLength(moduleList, 'branchName')
// Adjust spacing And Print Git Module, Branch, Version
moduleList.each{ Dependency module ->
    String moduleNameSpace = getSpacesToLineUp(module.name, bestLongModuleNameLength)
    String branchNameSpace = getSpacesToLineUp(module.branchName, bestLongBranchNameLength)
    println " - ${module.name}${moduleNameSpace} (${module.branchName})${branchNameSpace} Version. ${module.version}"
}

/*************************
 * Print Build Options
 *************************/
printSubject "Build Option"
// Print
println "[${modeRemoteDependencies?'v':' '}] Remote Dependencies Mode( -Premote )"

/*************************
 * Print Next
 *************************/
printSubject "Start Task"



/**************************************************
 *
 * Tasks 
 *
 **************************************************/
/*************************
 *
 * TASK - jar
 *
 *************************/
/** Before jar **/
task writeVersion {
    doLast{
        new File("$buildDir/resources/main/.version").write(version)
        new File("$buildDir/resources/main/.compiler").write("Java ${System.getProperty('java.version')}")
        new File("$buildDir/resources/main/.date").write(new Date().toString())
        new File("$buildDir/resources/main/.libtohome").write('../')
    }
}

jar {
    dependsOn writeVersion

    /** MANIFEST **/
    manifest {
        attributes  'Title': projectName, 'Version': version, 'Main-Class': mainClassName
    }

    /** Jarfile Name **/
    archiveName "${libFileName}.jar"

    /** Extracted All Dependencies To jar **/
//    dependsOn configurations.runtime
//    from {
//        configurations.compile.collect {it.isDirectory()? it: zipTree(it)}
//    }

    /** All Dependencies to lib folder in jar **/
//    into('lib') {
//        from configurations.runtime
//    }
}

/*************************
 *
 * TASK - distZIp
 *
 *************************/
/** Before distZip **/
applicationDistribution.from([
        "src/main/resources/binForBuilder/macgyver",
        "src/main/resources/binForBuilder/macgyver.bat",
]){
    into "bin"
}

distZip {
    archiveName "${libFileName}.zip"
}

/*************************
 *
 * TASK - distTar
 *
 *************************/
distTar {
    archiveName "${libFileName}.tar"
}


/*************************
 *
 * TASK - build
 *
 *************************/
/** Before build **/
task extractZipToTemp(type: Copy){
    from zipTree("${distributionPath}/${libFileName}.zip")
    into "${distributionPath}"
    doLast{
        file("${distributionPath}/${libFileName}").renameTo(file("${distributionPath}/${projectName}"))
    }
}

build.dependsOn extractZipToTemp
build.doLast {
    println "\n#########################"
    println "##### Check Distributions (Path: ${distributionPath} )"
    println "#########################"
    new File(distributionPath).listFiles().each{ println it }
    println ""
}


/*************************
 *
 * TASK - deployLocal
 *  - Deploy to INSTALLER_MAKER_HOME
 *
 *************************/
/** Before deployLocal **/
task cleanHome(type: Delete){
    hadDeployment = new File(SystemEnv_INSTALLER_MAKER_HOME).exists()
    delete SystemEnv_INSTALLER_MAKER_HOME
    doLast{
        if (hadDeployment){
            println "\n#########################"
            println "##### Deleted Deployment!!! (Path: ${SystemEnv_INSTALLER_MAKER_HOME} )"
            println "#########################"
            println ""
        }
    }
}

task deployLocal(type: Copy){
    dependsOn distZip, extractZipToTemp, cleanHome
    from "${distributionPath}/${projectName}"
    into SystemEnv_INSTALLER_MAKER_HOME
    doLast{
        println "\n#########################"
        println "##### ${(hadDeployment)?'Re-Deployed':'Newly-Deployed'} !!! (Path: ${SystemEnv_INSTALLER_MAKER_HOME} )"
        println "#########################"
        new File(SystemEnv_INSTALLER_MAKER_HOME).listFiles().each{ println it }
        println ""
    }
}


