apply plugin: "groovy"
apply plugin: "java"
apply plugin: "application"
apply plugin: "idea"

/*************************
 *
 * Define Variable&Function
 * 
 *************************/
sourceCompatibility = "1.6"
targetCompatibility = "1.6"

String projectName = "installer-maker"
version = "git describe --tags".execute([], project.projectDir).text.trim()
applicationName = projectName
group = "jaemmisseo"
mainClassName = "install.Starter"
String distributionPath = "${buildDir}/distributions"
String libFileName = "${projectName}-${version}"
String SystemEnv_INSTALLER_MAKER_HOME = "$System.env.INSTALLER_MAKER_HOME"
boolean hadDeployment = false

ext.getBoolean = { String propertyName ->
    boolean result
    if (project.hasProperty('dev')){
        String value = project.property('dev')
        result = (!value || value == 'true')
    }else{
        result = false
    }
    return result
}

ext.checkProject = { String subProjectName ->
    return (getBoolean('dev') && rootProject?.findProject(subProjectName))
}

ext.getDependency = { String dependencyProjectName ->
    List projectItems = dependencyProjectName.split(':').toList()
    String path = projectItems[0]
    String name = projectItems[1]
    String version = projectItems[2]?:''
    return checkProject(name) ? project(":${name}") : dependencyProjectName
}

/*************************
 * Develop Mode
 *************************/
if (getBoolean('dev')){
    println "#########################"
    println "#####"
    println "##### Development Mode"
    println "#####"
    println "#########################"
}

        
/*************************
 *
 * SETUP
 *
 *************************/
buildscript {
    ext {
        groovyVersion = '2.1.3'
    }
    repositories {
        mavenCentral()
        maven {
            url 'http://repo.spring.io/milestone'
        }
    }
    dependencies {
//        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.12'
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}



/*************************
 * 
 * SOURCE SET
 *
 *************************/
sourceSets {
    main {
        groovy {
            srcDirs = ['src/main/java']
        }
    }

    test {
        groovy {
            srcDirs = ['src/test/java']
        }
    }
}



/*************************
 *
 * JAR
 *
 *************************/
/** Before jar **/
task writeVersion {
    doLast{
        new File("$buildDir/resources/main/.version").write(version)
        new File("$buildDir/resources/main/.compiler").write("Java ${System.getProperty('java.version')}")
        new File("$buildDir/resources/main/.date").write(new Date().toString())
        new File("$buildDir/resources/main/.libtohome").write('../')
    }
}

jar {
    dependsOn writeVersion

    /** MANIFEST **/
    manifest {
        attributes  'Title': projectName, 'Version': version, 'Main-Class': mainClassName
    }

    /** Jarfile Name **/
    archiveName "${libFileName}.jar"

    /** Extracted All Dependencies To jar **/
//    dependsOn configurations.runtime
//    from {
//        configurations.compile.collect {it.isDirectory()? it: zipTree(it)}
//    }

    /** All Dependencies to lib folder in jar **/
//    into('lib') {
//        from configurations.runtime
//    }
}

/*************************
 *
 * DIST ZIP
 *
 *************************/
/** Before distZip **/
applicationDistribution.from([
        "src/main/resources/binForBuilder/macgyver",
        "src/main/resources/binForBuilder/macgyver.bat",
]){
    into "bin"
}

distZip {
    archiveName "${libFileName}.zip"
}

/*************************
 *
 * DIST TAR
 *
 *************************/
distTar {
    archiveName "${libFileName}.tar"
}


/*************************
 *
 * BUILD
 *
 *************************/
/** Before build **/
task extractZipToTemp(type: Copy){
    from zipTree("${distributionPath}/${libFileName}.zip")
    into "${distributionPath}"
    doLast{
        file("${distributionPath}/${libFileName}").renameTo(file("${distributionPath}/${projectName}"))
    }
}

build.dependsOn extractZipToTemp
build.doLast {
    println "\n#########################"
    println "##### Check Distributions (Path: ${distributionPath} )"
    println "#########################"
    new File(distributionPath).listFiles().each{ println it }
    println ""
}


/*************************
 *
 * deployLocal
 *  - Deploy to INSTALLER_MAKER_HOME
 *
 *************************/
/** Before deployLocal **/
task cleanHome(type: Delete){
    hadDeployment = new File(SystemEnv_INSTALLER_MAKER_HOME).exists()
    delete SystemEnv_INSTALLER_MAKER_HOME
    doLast{
        if (hadDeployment){
            println "\n#########################"
            println "##### Deleted Deployment!!! (Path: ${SystemEnv_INSTALLER_MAKER_HOME} )"
            println "#########################"
            println ""
        }
    }
}

task deployLocal(type: Copy){
    dependsOn distZip, extractZipToTemp, cleanHome
    from "${distributionPath}/${projectName}"
    into SystemEnv_INSTALLER_MAKER_HOME
    doLast{
        println "\n#########################"
        if (hadDeployment){
            println "##### Re-Deployed !!! (Path: ${SystemEnv_INSTALLER_MAKER_HOME} )"
        }else{
            println "##### Newly-Deployed !!! (Path: ${SystemEnv_INSTALLER_MAKER_HOME} )"
        }
        println "#########################"
        new File(SystemEnv_INSTALLER_MAKER_HOME).listFiles().each{ println it }
        println ""
    }
}



/*************************
 *
 * Dependencies
 *
 *************************/
repositories {
    jcenter()
    maven { url "https://jitpack.io" }
    maven { url 'http://repo.spring.io/milestone' }
    maven { url "http://maven.springframework.org/release" }
}

dependencies {
    //JAEMISSEO
    compile( getDependency('com.github.avaj-java:common-util:0.1.62') )
    compile( getDependency('com.github.avaj-java:common-man:0.1.63') )
    compile( getDependency('com.github.avaj-java:fileman:0.1.63') )
    compile( getDependency('com.github.avaj-java:reportman:0.0.1') )
    compile( getDependency('com.github.avaj-java:restman:0.0.1') )

    //GROOVY
    compile 'org.codehaus.groovy:groovy-all:2.1.3'

    //MAIL
    compile 'com.sun.mail:javax.mail:1.5.5'

    //JDBC
    compile "com.oracle:ojdbc6:11.2.0.3"

    //TEST
    testCompile "junit:junit:4.11"
}

